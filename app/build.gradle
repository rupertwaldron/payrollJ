plugins {
    id 'com.ruppyrup.java-application-conventions'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id "com.dorongold.task-tree" version "2.1.1"
}

group 'com.ruppyrup.app'
version '0.0.1-SHAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation project(':operations')
    implementation project(':union')
    implementation project(':policy')

    testImplementation project(':persistance')
    testImplementation project(':core')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

testSets {
    integrationTest
}

integrationTest {
    useJUnitPlatform()
}

tasks.named('check') {
    dependsOn tasks.named('integrationTest')
}

tasks.register('runJar', JavaExec) {
    classpath tasks.named('jar').map { it.outputs }
    classpath configurations.runtimeClasspath
    mainClass = 'com.ruppyrup.app.PayrollApplication'
}